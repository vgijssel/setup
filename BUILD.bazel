load("//tools/command:command.bzl", "command")
load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@npm//:prettier/package_json.bzl", prettier_bin = "bin")

package(default_visibility = ["//visibility:public"])

exports_files(["WORKSPACE"])

npm_link_all_packages(name = "node_modules")

bool_flag(
    name = "setup_debug_flag",
    build_setting_default = False,
)

config_setting(
    name = "setup_debug",
    flag_values = {
        "//:setup_debug_flag": "True",
    },
)

filegroup(
    name = "prettier-files",
    srcs = glob([
        "**/*.js",
        "**/*.ts",
        "**/*.tsx",
        "**/*.json",
        "**/*.md",
        "**/*.yml",
        "**/*.yaml",
    ]) + [
        ".prettierignore",
        ".prettierrc",
    ],
)

prettier_bin.prettier_binary(
    name = "prettier_bin",
)

command(
    name = "prettier",
    command_src = ":prettier_bin",
    cwd = "{{ os.environ.get('BUILD_WORKING_DIRECTORY', os.getcwd()) }}",
    env = {
        "BAZEL_BINDIR": ".",
    },
)

prettier_bin.prettier_test(
    name = "prettier_test",
    args = [
        "--check",
        ".",
    ],
    data = [":prettier-files"],
)

filegroup(
    name = "bazel-files",
    srcs = glob([
        "**/*.bazel",
        "**/WORKSPACE",
        "**/WORKSPACE.bzlmod",
        "**/*.bzl",
    ]),
)
