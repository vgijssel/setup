load("@bazel_skylib//rules:common_settings.bzl", "bool_flag")
load("@rules_task//tools:defs.bzl", "compile_pip_requirements")
load("@rules_task//:defs.bzl", "cmd", "task", "task_test")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_tarball")
load("@bazel_tools//tools/python:toolchain.bzl", "py_runtime_pair")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_binary")

package(default_visibility = ["//visibility:public"])

exports_files(["WORKSPACE"])

bool_flag(
    name = "setup_debug_flag",
    build_setting_default = False,
)

config_setting(
    name = "setup_debug",
    flag_values = {
        "//:setup_debug_flag": "True",
    },
)

config_setting(
    name = "is_linux_amd64",
    constraint_values = [
        "@platforms//cpu:x86_64",
        "@platforms//os:linux",
    ],
)

config_setting(
    name = "is_linux_arm64",
    constraint_values = [
        "@platforms//cpu:aarch64",
        "@platforms//os:linux",
    ],
)

config_setting(
    name = "is_darwin_arm64",
    constraint_values = [
        "@platforms//cpu:aarch64",
        "@platforms//os:macos",
    ],
)

compile_pip_requirements(
    name = "requirements",
    extra_args = [
        "--allow-unsafe",
    ],
    hidden_args = [
        "--verbose",
    ],
    requirements_in = "requirements.in",
    requirements_txt = "requirements.txt",
)

constraint_setting(name = "shine_containerized")

constraint_value(
    name = "shine_run_in_container",
    constraint_setting = ":shine_containerized",
)

platform(
    name = "shine_platform",
    constraint_values = [
        ":shine_run_in_container",
    ],
)

py_runtime(
    name = "shine_container_py2_runtime",
    interpreter_path = "/usr/bin/python3",
    python_version = "PY2",
)

py_runtime(
    name = "shine_container_py3_runtime",
    interpreter_path = "/usr/bin/python3",
    python_version = "PY3",
)

py_runtime_pair(
    name = "shine_container_py_runtime_pair",
    py2_runtime = ":shine_container_py2_runtime",
    py3_runtime = ":shine_container_py3_runtime",
)

toolchain(
    name = "shine_container_py_toolchain",
    target_compatible_with = [
        ":shine_run_in_container",
    ],
    toolchain = ":shine_container_py_runtime_pair",
    toolchain_type = "@bazel_tools//tools/python:toolchain_type",
)

sh_binary(
    name = "setup_ci",
    srcs = [
        "setup_ci.sh",
    ],
)

load("//tools/python:py_image_layer.bzl", "py_image_layer")

task(
    name = "hello_world",
    cmds = [
        "echo 'hello world'",
    ],
)

platform_transition_binary(
    name = "hello_world_transitioned",
    basename = "hello_world",
    binary = ":hello_world",
    target_platform = ":shine_platform",
)

py_image_layer(
    name = "hello_world_layer",
    binary = ":hello_world_transitioned",
    prefix = "opt/",
)

oci_image(
    name = "hello_world_image",
    base = "@python38_base_image//:image.tar.gz",
    entrypoint = ["/opt/hello_world_transitioned/hello_world"],
    tars = [
        ":hello_world_layer",
    ],
)

oci_tarball(
    name = "hello_world_tarball",
    image = ":hello_world_image",
    repo_tags = ["hello_world:latest"],
)

task(
    name = "hello_world_image_run",
    cmds = [
        "docker load < $TARBALL",
        "docker run --rm -it --entrypoint='' hello_world:latest  $CLI_ARGS",
    ],
    env = {
        "TARBALL": cmd.file(":hello_world_tarball"),
    },
)

task_test(
    name = "hello_world_test",
    cmds = [
        "docker load < $TARBALL",
        "docker run --rm --entrypoint='' hello_world:latest find /opt",
        "docker run --rm  hello_world:latest",
    ],
    env = {
        "TARBALL": cmd.file(":hello_world_tarball"),
    },
    exec_properties = {
        "workload-isolation-type": "firecracker",
        "init-dockerd": "true",
        "recycle-runner": "true",
    },
)
