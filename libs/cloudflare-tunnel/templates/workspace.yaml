apiVersion: opentofu.upbound.io/v1beta1
kind: Workspace
metadata:
  name: cloudflare-tunnel-workspace
spec:
  forProvider:
    source: Inline
    module: |
      terraform {
        required_providers {
          cloudflare = {
            source = "cloudflare/cloudflare"
            version = "5.9.0"
          }
        }
      }

      variable "cloudflare_api_token" {
        description = "Cloudflare API token"
        type        = string
        sensitive   = true
      }

      variable "cloudflare_account_id" {
        description = "Cloudflare account ID"
        type        = string
        sensitive   = true
      }

      variable "tunnel_name" {
        description = "Cloudflare tunnel name"
        type        = string
      }

      variable "tunnel_service" {
        description = "Service URL for the tunnel"
        type        = string
      }

      variable "zone_name" {
        description = "Cloudflare zone name"
        type        = string
      }

      locals {
        tunnel_hostname = "${var.tunnel_name}.${var.zone_name}"
      }

      provider "cloudflare" {
        api_token = var.cloudflare_api_token
      }

      resource "cloudflare_zero_trust_tunnel_cloudflared" "example" {
        account_id = var.cloudflare_account_id
        name       = var.tunnel_name
      }

      resource "cloudflare_zero_trust_tunnel_cloudflared_config" "example" {
        account_id = var.cloudflare_account_id
        tunnel_id  = cloudflare_zero_trust_tunnel_cloudflared.example.id

        config = {
          ingress = [
            {
              hostname = local.tunnel_hostname
              service  = var.tunnel_service
            },
            {
              service = "http_status:404"
            }
          ]
        }
      }

      data "cloudflare_zero_trust_tunnel_cloudflared_token" "example" {
        account_id = var.cloudflare_account_id
        tunnel_id  = cloudflare_zero_trust_tunnel_cloudflared.example.id
      }

      resource "cloudflare_dns_record" "tunnel_cname" {
        zone_id = data.cloudflare_zone.vgijssel_dev.zone_id
        name    = local.tunnel_hostname
        ttl     = 1
        type    = "CNAME"
        proxied = true
        content = "${cloudflare_zero_trust_tunnel_cloudflared.example.id}.cfargotunnel.com"
      }

      data "cloudflare_zones" "available" {
      }

      data "cloudflare_zone" "vgijssel_dev" {
        filter = {
          name = var.zone_name
        }
      }

      output "available_zones" {
        value = data.cloudflare_zones.available.result
      }

      output "tunnel_id" {
        value = cloudflare_zero_trust_tunnel_cloudflared.example.id
      }

      output "tunnel_token" {
        value     = data.cloudflare_zero_trust_tunnel_cloudflared_token.example.token
        sensitive = true
      }
    vars:
      - key: tunnel_name
        value: "{{ .Values.tunnel.name }}"
      - key: tunnel_service
        value: "{{ .Values.tunnel.service }}"
      - key: zone_name
        value: "{{ .Values.tunnel.zone }}"
    env:
      - name: TF_VAR_cloudflare_api_token
        secretKeyRef:
          name: cloudflare-tunnel
          key: credential
          namespace: "{{ .Release.Namespace }}"
      - name: TF_VAR_cloudflare_account_id
        secretKeyRef:
          name: cloudflare-tunnel
          key: username
          namespace: "{{ .Release.Namespace }}"
  writeConnectionSecretToRef:
    name: cloudflare-tunnel-outputs
    namespace: "{{ .Release.Namespace }}"
  providerConfigRef:
    name: default
