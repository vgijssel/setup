load("@aspect_bazel_lib//lib:tar.bzl", "tar")
load("@rules_release//tools:defs.bzl", "publish_github_release", release = "release_changed_files")
load("//task:defs.bzl", "cmd")
load("//tools:defs.bzl", "compile_pip_requirements")

package(default_visibility = ["//visibility:public"])

compile_pip_requirements(
    name = "requirements",
    extra_args = ["--allow-unsafe"],
    requirements_in = "requirements.in",
    requirements_txt = "requirements.txt",
)

filegroup(
    name = "all_files",
    srcs = glob(["**/*"]) + [
        "//task:all_files",
        "//task/private:all_files",
        "//tests:all_files",
        "//tools:all_files",
    ],
)

tar(
    name = "release_archive",
    srcs = [
        ":all_files",
    ],
    out = "rules_task.tar.gz",
    compress = "gzip",
)

genrule(
    name = "github_release_template",
    srcs = [
        ":release.version_changelog",
        ":release.version",
        ":release_archive",
    ],
    outs = [
        "github_release_template.txt",
    ],
    cmd = """
VERSION="v$$(cat $(location :release.version))"
SHA=$$(shasum -a 256 $(location :release_archive) | awk '{print $$1}')
PREFIX="rules_task-$${VERSION}"
ARCHIVE="rules_task.tar.gz"

cat <<EOF > $(OUTS)
## Using WORKSPACE:

\\`\\`\\`starlark
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "rules_task",
    sha256 = "$${SHA}",
    url = "https://github.com/vgijssel/setup/releases/download/$${PREFIX}/$${ARCHIVE}",
)
\\`\\`\\`
EOF

cat $(location :release.version_changelog) >> $(OUTS)
    """,
)

publish_github_release(
    name = "publish_github_release",
    assets = [
        ":release_archive",
    ],
    before_cmds = [
        "export OP_SERVICE_ACCOUNT_TOKEN=${ONEPASSWORD_SERVICE_ACCOUNT_TOKEN_PROD:-}",
        "GH_TOKEN=$($OP read op://vgijssel-prod/github-release/credential)",
        "export GH_TOKEN",
    ],
    changelog_file = ":github_release_template",
    env = {
        "OP": cmd.executable("@rules_release//libs/onepassword:op"),
    },
    release = ":release",
)

release(
    name = "release",
    changed_file_paths = [
        "rules/rules_task",
    ],
    changelog_file = "CHANGELOG.md",
    publish_cmds = [
        ":publish_github_release",
    ],
    release_name = "rules_task",
    version_file = "version.txt",
)
