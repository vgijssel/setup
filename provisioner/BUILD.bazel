load("//tools/pyinfra:defs.bzl", "pyinfra_run")
load("@rules_task//:defs.bzl", "cmd", "task", "task_test")
load("//tools/docker:docker.bzl", "docker_load")
load("@pip-setup//:requirements.bzl", "requirement")

pyinfra_run(
    name = "provision",
    srcs = [
        "connectors/teleport.py",
        "deploys/microk8s/tasks/install_microk8s.py",
        "deploys/monitoring/tasks/install_monitoring.py",
        "deploys/network/tasks/install_network.py",
        "deploys/teleport/tasks/install_teleport.py",
        "group_data/dev.py",
        "group_data/prod.py",
        "group_data/test.py",
    ],
    args = [
        "--data install_network=True",
        "--data install_monitoring=True",
        "--data install_microk8s=True",
        "--data install_teleport=True",
    ],
    data = [
        "deploys/microk8s/files/cmdline.txt",
        "deploys/monitoring/files/telegraf.conf",
        "deploys/network/files/99_config.yaml",
        "deploys/teleport/files/teleport.yaml.j2",
    ],
    deploy = "deploy.py",
    env = {
        "TELEPORT_TSH_BINARY": cmd.executable("//tools/teleport:tsh"),
    },
    inventory = "inventory.py",
)

task(
    name = "deploy",
    cmds = [
        "export TELEPORT_IDENTITY=$(mktemp)",
        {"defer": "rm -f $TELEPORT_IDENTITY"},
        'echo "$TELEPORT_BUILDBUDDY_IDENTITY" > $TELEPORT_IDENTITY',
        cmd.executable(":provision"),
    ],
    env = {
        "SETUP_ENV": "prod",
    },
    exec_properties = {
        "include-secrets": "true",
    },
)

docker_load(
    name = "load_dev_image",
    exec_properties = {
        "workload-isolation-type": "firecracker",
        "init-dockerd": "true",
        "recycle-runner": "true",
    },
    image = "//tools/ubuntu:ubuntu_snap_base_image",
)

task(
    name = "run_dev_image",
    cmds = [
        "export CONTAINER_IMAGE=$($load_dev_image)",
        cmd.shell(
            "docker run",
            "--rm",
            "--detach",
            "--publish 127.0.0.1:10443:443",
            "--tmpfs /run",
            "--tmpfs /run/lock",
            "--tmpfs /tmp",
            "--privileged",
            "-v /lib/modules:/lib/modules:ro",
            "-h provisioner",
            "$CLI_ARGS",
            "$CONTAINER_IMAGE",
            "/sbin/init",
        ),
    ],
    env = {
        "load_dev_image": cmd.executable(":load_dev_image"),
    },
    exec_properties = {
        "workload-isolation-type": "firecracker",
        "init-dockerd": "true",
        "recycle-runner": "true",
    },
)

task(
    name = "dev",
    cmds = [
        "docker rm -f provisioner_dev",
        "export CONTAINER_ID=$($run_dev_image)",
        {"defer": "docker rm -f $CONTAINER_ID"},
        "docker logs -f $CONTAINER_ID",
    ],
    env = {
        "run_dev_image": cmd.shell(
            cmd.executable(":run_dev_image"),
            "--name provisioner_dev",
        ),
    },
)

task(
    name = "validate",
    cmds = [
        cmd.python("""
        import os
        setup_env = os.environ.get("SETUP_ENV", 'dev')

        if setup_env == 'test':
            os.environ['VALIDATE_HOST'] = f"docker://root@{os.environ['CONTAINER_ID']}"
        elif setup_env == 'prod':
            os.environ['VALIDATE_HOST'] = f"ssh://ubuntu@192.168.1.31"
        else:
            os.environ['VALIDATE_HOST'] = f"docker://root@provisioner_dev"
        """),
        cmd.python_entry_point("pytest:console_main", "-vv", "-ra", "--hosts=\"$VALIDATE_HOST\"", "$tests"),
    ],
    env = {
        "tests": cmd.files("test_provisioner.py"),
    },
    deps = [
        requirement("pytest-testinfra"),
        requirement("pyyaml"),
        requirement("semver"),
    ],
)

task_test(
    name = "test",
    cmds = [
        "export CONTAINER_ID=$($run_dev_image)",
        {"defer": "docker rm -f $CONTAINER_ID"},
        "$provision",
        "$validate",
    ],
    env = {
        "run_dev_image": cmd.executable(":run_dev_image"),
        "provision": cmd.executable(":provision"),
        "validate": cmd.executable(":validate"),
        "SETUP_ENV": "test",
    },
    exec_properties = {
        "workload-isolation-type": "firecracker",
        "init-dockerd": "true",
        "recycle-runner": "true",
    },
)
