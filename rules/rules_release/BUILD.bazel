load("@npm//:defs.bzl", "npm_link_all_packages")
load("@aspect_rules_js//js:defs.bzl", "js_binary")
load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_task//task:defs.bzl", "cmd")
load("//release:defs.bzl", "publish_github_release", "release")
load("@aspect_bazel_lib//lib:tar.bzl", "mtree_spec", "tar")

package(default_visibility = ["//visibility:public"])

_darwin_arm64 = [
    "@platforms//os:macos",
    "@platforms//cpu:arm64",
]

_linux_amd64 = [
    "@platforms//os:linux",
    "@platforms//cpu:x86_64",
]

_linux_arm64 = [
    "@platforms//os:linux",
    "@platforms//cpu:arm64",
]

config_setting(
    name = "is_linux_amd64",
    constraint_values = _linux_amd64,
)

config_setting(
    name = "is_linux_arm64",
    constraint_values = _linux_arm64,
)

config_setting(
    name = "is_darwin_arm64",
    constraint_values = _darwin_arm64,
)

npm_link_all_packages()

java_binary(
    name = "bazel-diff",
    main_class = "com.bazel_diff.Main",
    runtime_deps = ["@bazel_diff//jar"],
)

js_binary(
    name = "cli",
    data = [
        "lib/actions/GenerateAction.mjs",
        "lib/actions/PublishAction.mjs",
        "lib/actions/VersionAction.mjs",
        "lib/repositories/ChangelogRepository.mjs",
        "lib/repositories/ChangesetRepository.mjs",
        "lib/repositories/ConfigRepository.mjs",
        "lib/repositories/PackageRepository.mjs",
        "lib/repositories/PublishRepository.mjs",
        "lib/repositories/ReleaseRepository.mjs",
        "lib/repositories/TargetRepository.mjs",
        "lib/repositories/VersionRepository.mjs",
        "lib/utils.mjs",
        "//:node_modules/@changesets/write",
        "//:node_modules/commander",
        "//:node_modules/zx",
    ],
    entry_point = "cli.mjs",
    env = {
        "WORKSPACE_NAME": repository_name().removeprefix("@"),
    },
)

js_binary(
    name = "changesets_cli",
    data = [
        "//:node_modules/@changesets/changelog-github",
        "//:node_modules/@changesets/cli",
        "//:node_modules/zx",
    ],
    entry_point = "changesets_cli.mjs",
)

filegroup(
    name = "all_files",
    srcs = glob(["**/*"]) + [
        "//release:all_files",
        "//release/private:all_files",
    ],
)

mtree_spec(
    name = "release_archive_mtree",
    srcs = [
        ":all_files",
    ],
)

# Remove `../rules_release~override` from each line because otherwise
# this results in an empty archive when extracting.
genrule(
    name = "release_archive_mtree_normalized",
    srcs = [
        ":release_archive_mtree",
    ],
    outs = [
        "release_archive_mtree_normalized.spec",
    ],
    cmd = """
    sed 's|../{}/||' $< > $@
    """.format(repository_name().removeprefix("@")),
)

tar(
    name = "release_archive",
    srcs = [
        ":all_files",
    ],
    out = "rules_release.tar.gz",
    compress = "gzip",
    mtree = ":release_archive_mtree_normalized",
)

genrule(
    name = "github_release_template",
    srcs = [
        ":release.version_changelog",
        ":release.version",
        ":release_archive",
    ],
    outs = [
        "github_release_template.txt",
    ],
    cmd = """
export VERSION="v$$(cat $(location :release.version))"
export SHA=$$(shasum -a 256 $(location :release_archive) | awk '{print $$1}')
export PREFIX="rules_release-$${VERSION}"
export ARCHIVE="rules_release.tar.gz"

cat <<EOF > $(OUTS)
## Using WORKSPACE:

\\`\\`\\`starlark
load("@bazel_tools//tools/build_defs/repo:http.bzl", "http_archive")

http_archive(
    name = "rules_release",
    sha256 = "$${SHA}",
    url = "https://github.com/vgijssel/setup/releases/download/$${PREFIX}/$${ARCHIVE}",
)
\\`\\`\\`
EOF

cat $(location :release.version_changelog) >> $(OUTS)
    """,
)

publish_github_release(
    name = "publish_github_release",
    assets = [
        ":release_archive",
    ],
    before_cmds = [
        "export OP_SERVICE_ACCOUNT_TOKEN=${ONEPASSWORD_SERVICE_ACCOUNT_TOKEN_PROD:-}",
        "export GH_TOKEN=$($OP read op://vgijssel-prod/github-release/credential)",
    ],
    changelog_file = ":github_release_template",
    env = {
        "OP": cmd.executable("//tools/onepassword:op"),
    },
    release = ":release",
)

release(
    name = "release",
    changelog_file = "CHANGELOG.md",
    publish_cmds = [
        ":publish_github_release",
    ],
    release_name = "rules_release",
    target = ":all_files",
    version_file = "version.txt",
)
