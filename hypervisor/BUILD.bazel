load("//tools/packer:defs.bzl", "packer_image")
load("//tools/pyinfra:defs.bzl", "pyinfra_run")
load("//tools/vagrant:defs.bzl", "qcow_to_vagrant_box")
load("//tools/bazel:defs.bzl", "runner_binary")
load("@rules_python//python:pip.bzl", "compile_pip_requirements")

exports_files([".kitchen"])

compile_pip_requirements(
    name = "requirements",
    extra_args = ["--allow-unsafe"],
    requirements_in = "requirements.txt",
    requirements_txt = "requirements_lock.txt",
)

packer_image(
    name = "hypervisor_image",
    output_image = "output/hypervisor.qcow2",
    templates = [
        "packer_qemu.pkr.hcl",
        "hypervisor.pkr.hcl",
    ],
    tools = [
        ":provision",
    ],
    variables = {
        "iso_file": "$(location @ubuntu_focal//file)",
        "iso_checksum": "6e3ce31fe3a5523023650ba988c12d5fc2544bd0a95f435474841e2dec5836d9",
        "ssh_username": "packer",
        "ssh_password": "packer",
        "cloud_init_meta_data_file": "$(location cloud-init/meta-data)",
        "cloud_init_user_data_file": "$(location cloud-init/user-data)",
        "provision_script": "$(location :provision)",
    },
    deps = [
        "cloud-init/meta-data",
        "cloud-init/user-data",
        "@ubuntu_focal//file",
    ],
)

qcow_to_vagrant_box(
    name = "hypervisor_image_box",
    src = ":hypervisor_image",
    src_sha = ":hypervisor_image.sha512",
    tags = ["no-cache"],
)

runner_binary(
    name = "kitchen",
    out = "kitchen.sh",
    cmd = """
    export KITCHEN_ARGS="$$@"
    export KITCHEN_BINARY=$$(rlocation hypervisor_bundle/bin/kitchen)
    export KITCHEN_YAML=$$(rlocation setup/hypervisor/kitchen.yml)
    export VAGRANTFILE_CLOUD_INIT=$$(rlocation setup/hypervisor/Vagrantfile.cloud-init)
    export VAGRANT_EXPERIMENTAL=cloud_init,disks
    export VAGRANT_BOX=$$(rlocation setup/hypervisor/hypervisor_image_box.box)
    export CHEF_LICENSE=accept
    export TEST_DIR=$$(rlocation setup/hypervisor/test)
    export KITCHEN_CWD=$$(dirname $$KITCHEN_YAML)
    export CONVERGE_SCRIPT=$$(rlocation setup/tools/vagrant/multi-user-target-active.sh)

    cd $$KITCHEN_CWD && $$KITCHEN_BINARY $$KITCHEN_ARGS
    """,
    data = [
        "Vagrantfile.cloud-init",
        "kitchen.yml",
        "test",
        ":hypervisor_image_box",
        "//tools/vagrant:multi-user-target-active.sh",
        "@hypervisor_bundle//:bin/kitchen",
    ],
    deps = [],
)

pyinfra_run(
    name = "provision",
    args = ["--sudo"],
    data = [
        ".kitchen",
    ],
    deploy = "deploy.py",
    env = {
        # Specifying the environment variables necessary for the vagrant connector and the associated Vagrantfile.
        "VAGRANT_CWD": "$$(rlocation setup/hypervisor/.kitchen)/kitchen-vagrant/default-ubuntu-focal",
    },
    inventory = "inventory.py",
    pyinfra_runtime = "//tools/pyinfra:pyinfra_runtime",
    deps = [
        "files/ignited.service",
    ],
)
